apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'realm-android'
android {
    compileSdkVersion 34
    defaultConfig {
        applicationId "org.ole.planet.myplanet"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1759
        versionName "0.17.59"
        ndkVersion '21.3.6528147'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    configurations.configureEach {
        resolutionStrategy.force 'com.google.android.material:material:1.12.0'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        resources {
            excludes += [
                'META-INF/DEPENDENCIES',
                'META-INF/LICENSE',
                'META-INF/LICENSE.txt',
                'META-INF/license.txt',
                'META-INF/NOTICE',
                'META-INF/NOTICE.txt',
                'META-INF/notice.txt',
                'META-INF/ASL2.0'
            ]
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    lintOptions {
        abortOnError false
    }
    
    namespace 'org.ole.planet.myplanet'

    buildFeatures {
        viewBinding = true
        buildConfig true
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    def planetLearningUrl = project.findProperty('PLANET_LEARNING_URL') ?: ""
    def planetLearningPin = project.findProperty('PLANET_LEARNING_PIN') ?: ""
    def planetGuatemalaUrl = project.findProperty('PLANET_GUATEMALA_URL') ?: ""
    def planetGuatemalaPin = project.findProperty('PLANET_GUATEMALA_PIN') ?: ""
    def planetXelaUrl = project.findProperty('PLANET_XELA_URL') ?: ""
    def planetXelaPin = project.findProperty('PLANET_XELA_PIN') ?: ""
    def planetSanPabloUrl = project.findProperty('PLANET_SANPABLO_URL') ?: ""
    def planetSanPabloPin = project.findProperty('PLANET_SANPABLO_PIN') ?: ""

    buildTypes.each {
        it.buildConfigField 'String', 'PLANET_LEARNING_URL', "\"$planetLearningUrl\""
        it.buildConfigField 'String', 'PLANET_LEARNING_PIN', "\"${planetLearningPin}\""
        it.buildConfigField 'String', 'PLANET_GUATEMALA_URL', "\"${planetGuatemalaUrl}\""
        it.buildConfigField 'String', 'PLANET_GUATEMALA_PIN', "\"${planetGuatemalaPin}\""
        it.buildConfigField 'String', 'PLANET_XELA_URL', "\"${planetXelaUrl}\""
        it.buildConfigField 'String', 'PLANET_XELA_PIN', "\"${planetXelaPin}\""
        it.buildConfigField 'String', 'PLANET_SANPABLO_URL', "\"${planetSanPabloUrl}\""
        it.buildConfigField 'String', 'PLANET_SANPABLO_PIN', "\"${planetSanPabloPin}\""
    }
}

tasks.register('generateVersionsXml') {
    def versionName = android.defaultConfig.versionName
    doLast {
        def versionsXmlFile = file('src/main/res/values/versions.xml')
        versionsXmlFile.text = """<?xml version="1.0" encoding="utf-8"?>
<resources>
    <string name="app_version">$versionName</string>
</resources>"""
    }
}
preBuild.dependsOn generateVersionsXml

tasks.named('generateVersionsXml').configure {
    notCompatibleWithConfigurationCache("not supported due to internal project references")
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://maven.google.com" }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    implementation(project(':library'))  {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.annotation:annotation:1.8.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.7'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.core:core-ktx:1.13.1"
    implementation "androidx.work:work-runtime:2.9.0"
    implementation "androidx.slidingpanelayout:slidingpanelayout:1.2.0"
    implementation 'androidx.preference:preference-ktx:1.2.1'

    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.android:flexbox:1.0.0'

    implementation "com.github.VaibhavLakhera:Circular-Progress-View:0.1.2"
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.github.barteksc:android-pdf-viewer:3.2.0-beta.1'
    implementation 'com.github.jeancsanchez:jcplayer:2.7.2'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.github.kizitonwose:CalendarView:1.0.4'
    implementation 'com.applandeo:material-calendar-view:1.9.2'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'com.firebase:firebase-jobdispatcher:0.8.6'
    implementation 'com.borax12.materialdaterangepicker:library:2.0'
    implementation 'com.nex3z:toggle-button-group:1.2.3'
    implementation 'com.sa90.materialarcmenu:library:2.0.0'
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation('com.opencsv:opencsv:5.9') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'de.rtner:PBKDF2:1.1.4'
    implementation 'org.osmdroid:osmdroid-android:6.1.18'
    implementation 'org.jetbrains:annotations:24.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.1"
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.29'

    implementation 'com.mikepenz:google-material-typeface:3.0.1.1.original@aar'
    implementation 'com.mikepenz:fontawesome-typeface:5.3.1.1@aar'
    implementation 'com.mikepenz:octicons-typeface:3.2.0.5@aar'
    implementation 'com.mikepenz:itemanimators:1.1.0@aar'
    implementation 'com.mikepenz:crossfader:1.6.0@aar'
    implementation 'com.mikepenz:crossfadedrawerlayout:1.1.0@aar'
    implementation('com.mikepenz:materialdrawer:6.1.1@aar') { transitive = true}

    def camera_version = "1.3.4"
    implementation "androidx.camera:camera-core:$camera_version"
    implementation "androidx.camera:camera-camera2:$camera_version"
    implementation "androidx.camera:camera-lifecycle:$camera_version"
    implementation "androidx.camera:camera-view:$camera_version"

    def dagger_hilt_version = "2.51.1"
    implementation "com.google.dagger:hilt-android:$dagger_hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$dagger_hilt_version"

    def retrofit2_version = "2.11.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit2_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2_version"

    def glide_version = "4.16.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    def media3_version = "1.4.0"
    implementation "androidx.media3:media3-exoplayer:$media3_version"
    implementation "androidx.media3:media3-ui:$media3_version"
    implementation "androidx.media3:media3-common:$media3_version"

    def markwon_version = "4.6.2"
    implementation "io.noties.markwon:editor:$markwon_version"
    implementation "io.noties.markwon:image:$markwon_version"
    implementation "io.noties.markwon:html:$markwon_version"
    implementation "io.noties.markwon:ext-tables:$markwon_version"
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:2.0.0"))
}
realm {
    syncEnabled = true
}
kapt{
    correctErrorTypes  true
}
